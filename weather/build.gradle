if (weatherAsComponent.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

android {
    // 使用Java8新特性
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 对资源文件中的xml类型文件进行前缀检测，未包含该前缀，会有错误提示
    resourcePrefix('weather_')

    compileSdkVersion COMPILE_SDK_VERSION.toInteger()

    defaultConfig {
        if (!weatherAsComponent.toBoolean()) {
            applicationId "com.endymion.weather"
        }
        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TARGET_SDK_VERSION.toInteger()
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (weatherAsComponent.toBoolean()) {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
                // 组件模式下，用不到的类
                java {
                    exclude 'debug/**'
                }
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':common')
    testImplementation 'junit:junit:4.12'

    implementation "com.android.support:support-v4:$SUPPORT_V4_VERSION"
    implementation "com.android.support:appcompat-v7:$SUPPORT_APPCOMPAT_V7_VERSION"
    implementation "com.android.support.constraint:constraint-layout:$CONSTRAINT_LAYOUT_VERSION"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
